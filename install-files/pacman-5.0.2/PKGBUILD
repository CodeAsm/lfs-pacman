pkgname=pacman
pkgver=5.0.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
backup=(etc/pacman.conf etc/makepkg.conf)
source=(${pkgname}-${pkgver}.tar.gz
        pacman.conf.x86_64
        makepkg.conf)
sha256sums=(dfd36086ad68564bcd977f4a1fafe51dd328acd4a95093ac4bf1249be9c41f0e
    95b3b2416402059cf6acf3e046082e7ce261e2b88629231dbf579a4200d8a63b
    6066d67d818ee36760bf121c76d5019130f7875b3e5ed179b319b810a3a9685b
)

build() {
    cd "$pkgname-$pkgver"

    PKG_CONFIG=pkgconf ./configure              \
	--prefix=/tools        \
	--sysconfdir=/etc    \
	--localstatedir=/var \
	--disable-doc        \
    --disable-shared

    make V=1
    make -C contrib 
}

package() {
    cd "$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" -C contrib install

    # install Arch specific stuff
    install -dm755 "$pkgdir/etc"
    install -m644 "$srcdir/pacman.conf.$CARCH" "$pkgdir/etc/pacman.conf"

    case $CARCH in
	i686)
	    mycarch="i686"
	    mychost="i686-pc-linux-gnu"
	    myflags="-march=i686"
	    ;;
	x86_64)
	    mycarch="x86_64"
	    mychost="x86_64-pc-linux-gnu"
	    myflags="-march=x86-64"
	    ;;
    esac

    # set things correctly in the default conf file
    sed "$pkgdir/etc/makepkg.conf"       \
	-e "s|@CARCH[@]|$mycarch|g"      \
	-e "s|@CHOST[@]|$mychost|g"      \
	-e "s|@CARCHFLAGS[@]|$myflags|g" \
	> "$srcdir/makepkg.conf.new"
    install -m644 "$srcdir/makepkg.conf.new" "$pkgdir/etc/makepkg.conf"

    # put bash_completion in the right location
    install -dm755 "$pkgdir/usr/share/bash-completion/completions"
    mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
    rmdir "$pkgdir/etc/bash_completion.d"

    for f in makepkg pacman-key; do
	ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
    done

    install -Dm644 contrib/PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
}
